#!/usr/bin/env zsh

#
# Author: Steven Spasbo
#
# zshenv is sourced on all invocations of the shell.
#
# It should contain commands to set the PATH and other
# important environment variables.

# Need this so PATH doesn't break on OSX Sierra.
setopt NO_GLOBAL_RCS

# Set up tmp files
if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$LOGNAME"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path manpath PATH MANPATH

# Set the list of directories that Zsh searches for programs.
path=(/usr/local/{bin,sbin} $path)

# General path
[ -d "$HOME/.bin" ] && export PATH="$PATH:$HOME/.bin"

# cdpath
cdpath=(~)
[ -d /Work ] && cdpath+=(/Work)

# fpath
[ -d "$HOME/.zsh-custom/functions" ] && fpath=("$HOME/.zsh-custom/functions" $fpath)

# HELPDIR
[ -d /usr/local/share/zsh/help ] && export HELPDIR=/usr/local/share/zsh/help

# Ruby / rvm
function ruby-loader() {
  [ -s "$HOME/.rvm/scripts/rvm" ] && . "$HOME/.rvm/scripts/rvm"
}

# NVM / Node
function node-loader() {
  if [ -d "$HOME/.nvm" ]; then
    export NVM_DIR="$HOME/.nvm"
    source "$NVM_DIR/nvm.sh"
    nvm-update() {
      CURRENT_DIR=$(pwd)
      cd "$NVM_DIR"
      git fetch --tags > /dev/null
      TAG=$(git  describe --tags --abbrev=0)
      HEAD_SHORT_REV=$(git rev-parse --short HEAD)
      git checkout $TAG > /dev/null 2>&1

      if [[ $(git rev-parse --short HEAD) == $HEAD_SHORT_REV ]]; then
        echo "NVM already on most reccent tag."
      else
        echo "Checked out $TAG"
      fi
      cd "$CURRENT_DIR"
    }
  fi
}

#  Go
function go-loader() {
  [ -d "$HOME/.gvm" ] && source "$HOME/.gvm/scripts/gvm"
}

# Perl
function perl-loader() {
  if [ -d "$HOME/perl5" ]; then
    PATH="$HOME/perl5/bin${PATH:+:${PATH}}"; export PATH;
    PERL5LIB="$HOME/perl5/lib/perl5${PERL5LIB:+:${PERL5LIB}}"; export PERL5LIB;
    PERL_LOCAL_LIB_ROOT="$HOME/perl5${PERL_LOCAL_LIB_ROOT:+:${PERL_LOCAL_LIB_ROOT}}"; export PERL_LOCAL_LIB_ROOT;
    PERL_MB_OPT="--install_base \"$HOME/perl5\""; export PERL_MB_OPT;
    PERL_MM_OPT="INSTALL_BASE=$HOME/perl5"; export PERL_MM_OPT;
  fi
}

# Rust
function rust-loader() {
  if [ -d "$HOME/.cargo" ]; then
    export PATH="$PATH:$HOME/.cargo/bin"
  fi
}

# Python / pyenv
function python-loader() {
  # Python
  if [ -d "$HOME/.pyenv" ]; then
    export PYENV_ROOT="$HOME/.pyenv"
    export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init -)"
  fi
}

# Significantly increases load time by scheduling pyenv/nvm/rvm
# for a few seconds in the future.
zmodload zsh/sched

sched +5 python-loader
sched +7 node-loader
sched +10 ruby-loader
sched +10 rust-loader
sched +12 go-loader
sched +15 perl-loader

if [[ -d "$HOME/.local/bin" ]]; then
  export PATH="$HOME/.local/bin:$PATH"
fi

[[ -s "$HOME/.paths" ]] && source "$HOME/.paths"
