#!/usr/bin/env zsh

#
# Author: Steven Spasbo
#
# zshenv is sourced on all invocations of the shell.
#
# It should contain commands to set the PATH and other
# important environment variables.

# Need this so PATH doesn't break on OSX Sierra.
setopt NO_GLOBAL_RCS

# Set up tmp files
if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$LOGNAME"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path manpath

# Set up the path
path=(/usr/local/{bin,sbin} $path)

# For bash / zsh compatability, exports will go in a separate file.
[[ -s "$HOME/.exports" ]] && . "$HOME/.exports"

# cdpath
cdpath=(~)

# fpath
[ -d "$HOME/.zsh-custom/functions" ] && fpath+="$HOME/.zsh-custom/functions"

# HELPDIR
[ -d "/usr/local/share/zsh/help" ] && export HELPDIR="/usr/local/share/zsh/help"

# Significantly increases load time by scheduling pyenv/nvm/rvm
# for a few seconds in the future.
zmodload zsh/sched

# These come from the exports file.
sched +5 python-loader
sched +7 node-loader
sched +10 ruby-loader
sched +10 rust-loader
sched +12 go-loader
